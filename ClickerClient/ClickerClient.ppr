{
    Copyright (C) 2025 VCC
    creation date: Jul 2022
    initial release date: 26 Jul 2022

    author: VCC
    Permission is hereby granted, free of charge, to any person obtaining a copy
    of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:
    The above copyright notice and this permission notice shall be included
    in all copies or substantial portions of the Software.
    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
    DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
    TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
    OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}

library ClickerClient;

{$H+}
{$IFDEF FPC}
  //{$MODE Delphi}
{$ENDIF}

uses
  Interfaces,
  {$IFDEF Windows}
    Windows, ShellAPI,
  {$ELSE}
    LCLIntf, LCLType, Types, Math,
  {$ENDIF}
  SysUtils, Classes, Controls, Forms, Graphics,
  DllUtils, InMemFileSystem, ClickerActionsClient, ClickerUtils, ClickerTemplates,
  ClickerFileProviderClient, ClickerClientLoggingForm, ClickerClientAPI;


var
  InMemFS: TInMemFileSystem = nil;
  FRemoteAddress: string;
  FAllVarReplacements: TStringList = nil;
  FPollForMissingServerFilesTh: TPollForMissingServerFiles = nil;


type
  TExHandler = class
    procedure CustomExceptionHandler(Sender: TObject; E: Exception);
  end;

  TFileSenderHandler = class
    procedure HandleOnLoadMissingFileContent(AFileName: string; AFileContent: TMemoryStream);
    function HandleOnFileExists(const AFileName: string): Boolean;
  end;


var
  FExHandler: TExHandler;
  FFileSenderHandler: TFileSenderHandler;


procedure TExHandler.CustomExceptionHandler(Sender: TObject; E: Exception);
var
  ClName, EMsg: string;
  ExceptionLog: TStringList;
  LogPath: string;
begin
  try
    ClName := Sender.ClassName;
  except
    ClName := '';
  end;

  try
    EMsg := E.Message;
  except
    EMsg := '';
  end;

  try
    ExceptionLog := TStringList.Create;
    try
      ExceptionLog.Add(DateTimeToStr(Now));
      ExceptionLog.Add('Custom exception: ' + EMsg);
      ExceptionLog.Add('Class: ' + ClName);

      LogPath := ExtractFilePath(ParamStr(0)) + 'ClickerClientExceptions.txt';
      ExceptionLog.Add('This file was saved as: ' + LogPath);
      ExceptionLog.SaveToFile(LogPath);

      LogPath := '"' + LogPath + '"';
      {$IFDEF Windows}
        ShellExecute(0, 'open', 'C:\Windows\Notepad.exe', PChar(LogPath), '', SW_SHOW);
      {$ENDIF}

      //MessageBox(0, PChar(ExceptionLog.Text), 'Clicker', MB_ICONERROR);  //the application crashes after displaying the MessageBox, so the MessageBox is closed
    finally
      ExceptionLog.Free;
    end;
  except
    on EE: Exception do
      MessageBox(0, PChar('Ex on custom exception: ' + EE.Message), 'Clicker', MB_ICONERROR);
  end;
end;


//see https://wiki.freepascal.org/Logging_exceptions
procedure CatchUnhandledException(Obj: TObject; Addr: Pointer; FrameCount: LongInt; Frames: PPointer);
var
  Msg: string;
  i: Integer;
  ExceptionLog: TStringList;
  LogPath: string;
begin
  try //use another try..excempt, just in case parsing the stack would raise more exceptions
    Msg := 'Unhandled exception at adddr $' + SysBacktraceStr(Addr) + ':' + #13#10;

    if Obj is Exception then
      Msg := Msg + Exception(Obj).ClassName + ' : ' + Exception(Obj).Message
    else
      Msg := Msg + 'Exception object '  + Exception(Obj).ClassName + ' is not class Exception.';

    Msg := Msg + #13#10;
    Msg := Msg + BacktraceStrFunc(Addr) + #13#10;

    for i := 0 to FrameCount - 1 do
      Msg := Msg + BacktraceStrFunc(Frames[i]) + #13#10;

    ExceptionLog := TStringList.Create;
    try
      ExceptionLog.Add(DateTimeToStr(Now));
      ExceptionLog.Add('Unhandled exception: ' + Msg);

      LogPath := ExtractFilePath(ParamStr(0)) + 'ClickerClientUnhandledExceptions.txt';
      ExceptionLog.Add('This file was saved as: ' + LogPath);
      ExceptionLog.SaveToFile(LogPath);

      LogPath := '"' + LogPath + '"';

      {$IFDEF Windows}
        ShellExecute(0, 'open', 'C:\Windows\Notepad.exe', PChar(LogPath), '', SW_SHOW);
      {$ENDIF}

      //MessageBox(0, PChar(Msg), 'Clicker', MB_ICONERROR);
    finally
      ExceptionLog.Free;
    end;
  except
  end;
end;


procedure TFileSenderHandler.HandleOnLoadMissingFileContent(AFileName: string; AFileContent: TMemoryStream);
begin
  AFileContent.LoadFromFile(AFileName);
end;


function TFileSenderHandler.HandleOnFileExists(const AFileName: string): Boolean;
begin
  Result := FileExists(AFileName);
end;


procedure AddToLog(AMsg: string);
begin
  if frmClickerClientLogging = nil then
    Exit;

  frmClickerClientLogging.AddToLog(AMsg);
end;


procedure InitClickerClient; cdecl;
begin
  InMemFS := TInMemFileSystem.Create;
  FRemoteAddress := 'http://127.0.0.1:5444/';
  FAllVarReplacements := TStringList.Create;

  ExceptProc := @CatchUnhandledException;
  FExHandler := TExHandler.Create;  /////////////////////////// probably, it will have to be freed
  FFileSenderHandler := TFileSenderHandler.Create;

  Application.Initialize;
  Application.OnException := FExHandler.CustomExceptionHandler;

  FPollForMissingServerFilesTh := TPollForMissingServerFiles.Create(True);
  FPollForMissingServerFilesTh.RemoteAddress := FRemoteAddress;

  FPollForMissingServerFilesTh.OnLoadMissingFileContent := FFileSenderHandler.HandleOnLoadMissingFileContent;
  FPollForMissingServerFilesTh.OnFileExists := FFileSenderHandler.HandleOnFileExists;

  AddToLog('Clicker client is initialized. Remote address is set to ' + FRemoteAddress);
end;


procedure DoneClickerClient; cdecl;
begin
  FreeAndNil(InMemFS);
  FreeAndNil(FAllVarReplacements);
  FreeAndNil(FPollForMissingServerFilesTh);
  FreeAndNil(FFileSenderHandler);
end;


procedure SetServerAddress(AAddress: Pointer); cdecl;
begin
  SetPointedContentToString(@string(PWideChar(AAddress))[1], FRemoteAddress);

  if FPollForMissingServerFilesTh <> nil then
  begin
    FPollForMissingServerFilesTh.RemoteAddress := FRemoteAddress;
    AddToLog('Setting remote address to ' + FRemoteAddress);
  end
  else
    AddToLog('Can''t set remote address, because the file provider module is not creaeted.');
end;


function GetServerAddress(AResponse: Pointer): LongInt; cdecl;
begin
  try
    Result := SetPointedContentFromString(FRemoteAddress, AResponse);
  except
    on E: Exception do
      Result := SetPointedContentFromString(E.Message, AResponse);
  end;
end;


function TestConnectionToServer(AResponse: Pointer): LongInt; cdecl;
var
  Response: string;
begin
  try
    Response := SendTextRequestToServer(FRemoteAddress + CRECmd_TestConnection);
    Result := SetPointedContentFromString(Response, AResponse);
  except
    on E: Exception do
      Result := SetPointedContentFromString(E.Message, AResponse);
  end;
end;


procedure SetTemplatesDir(ADir: Pointer); cdecl;
var
  TempDir: string;
begin
  SetPointedContentToString(@string(PWideChar(ADir))[1], TempDir);

  if (TempDir <> '') and (TempDir[Length(TempDir)] = PathDelim) then
    Delete(TempDir, Length(TempDir) - 1, 1);

  if FPollForMissingServerFilesTh <> nil then
  begin
    FPollForMissingServerFilesTh.FullTemplatesDir := TempDir;
    AddToLog('Setting templates dir to "' + TempDir + '"');
  end;
end;


procedure CreateLoggingWindow; cdecl;
begin
  if frmClickerClientLogging = nil then
  begin
    Application.CreateForm(TfrmClickerClientLogging, frmClickerClientLogging);
    FPollForMissingServerFilesTh.OnLogMissingServerFile := frmClickerClientLogging.HandleOnLogMissingServerFile;
  end;

  frmClickerClientLogging.Visible := True;
  frmClickerClientLogging.BringToFront;
  AddToLog('Clicker client startup...');
end;


procedure DestroyLoggingWindow; cdecl;
begin
  if frmClickerClientLogging = nil then
    Exit;

  frmClickerClientLogging.Close;
  frmClickerClientLogging := nil;
end;


function CreateNewTemplateStr(ATemplateFileName: string): Integer;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  if InMemFS.FileExistsInMem(ATemplateFileName) then
  begin
    Result := 1;   /////////////////// ToDo: define some constants for standard responses
    Exit;
  end;

  InMemFS.SaveFileToMem(ATemplateFileName, nil, 0);
  Result := 0; //0 means ok
end;


//Creates a new template file, in dll's in-mem file system.
//This template (after being filled in) will have to be sent to server and executed there.
function CreateNewTemplate(ATemplateFileName: Pointer): LongInt; cdecl;
var
  TemplateFileName: string;
begin
  SetPointedContentToString(@string(PWideChar(ATemplateFileName))[1], TemplateFileName);
  Result := CreateNewTemplateStr(TemplateFileName);
end;


procedure AddActionToTemplate(ATemplateFileName: string; AClkAction: TClkActionRec);
var
  ClkActions: TClkActionsRecArr;
  MemStream: TMemoryStream;
  n: Integer;
  DummyNotes, DummyIconPath: string;
begin
  if not InMemFS.FileExistsInMem(ATemplateFileName) then
    CreateNewTemplateStr(ATemplateFileName);

  MemStream := TMemoryStream.Create;
  try
    InMemFS.LoadFileFromMemToStream(ATemplateFileName, MemStream);
    MemStream.Position := 0;

    GetTemplateContentFromMemoryStream(ClkActions, DummyNotes, DummyIconPath, MemStream);

    n := Length(ClkActions);
    SetLength(ClkActions, n + 1);
    CopyActionContent(AClkAction, ClkActions[n]);

    MemStream.Clear;
    GetTemplateContentAsMemoryStream(ClkActions, DummyNotes, DummyIconPath, MemStream);
    InMemFS.SaveFileToMem(ATemplateFileName, MemStream.Memory, MemStream.Size);
  finally
    MemStream.Free;
  end;
end;


procedure SetBasicActionOptions(ATemplateFileName: Pointer;
                                AActionName: Pointer;
                                Action: TClkAction;
                                AActionTimeout: LongInt; //ms
                                AActionEnabled: Boolean;
                                AActionCondition: Pointer;
                                var ATempAction: TClkActionRec;
                                var TemplateFileName: string);
var
  ActionName: string;
  ActionCondition: string;
begin
  SetPointedContentToString(@string(PWideChar(ATemplateFileName))[1], TemplateFileName);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  SetPointedContentToString(@string(PWideChar(AActionCondition))[1], ActionCondition);

  ATempAction.ActionOptions.ActionName := ActionName;
  ATempAction.ActionOptions.Action := Action;
  ATempAction.ActionOptions.ActionTimeout := AActionTimeout;
  ATempAction.ActionOptions.ActionEnabled := AActionEnabled;
  ATempAction.ActionOptions.ActionCondition := ActionCondition;
end;


function AddClickActionToTemplate(ATemplateFileName: Pointer;

                                  AActionName: Pointer;
                                  AActionTimeout: LongInt; //ms
                                  AActionEnabled: Boolean;
                                  AActionCondition: Pointer;

                                  AClickOptions: PClkClickOptionsAPI

                                  ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acClick, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetClickOptionsFromAPI(AClickOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddExecAppActionToTemplate(ATemplateFileName: Pointer;

                                    AActionName: Pointer;
                                    AActionTimeout: LongInt; //ms
                                    AActionEnabled: Boolean;
                                    AActionCondition: Pointer;

                                    AExecAppOptions: PClkExecAppOptionsAPI

                                   ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acExecApp, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetExecAppOptionsFromAPI(AExecAppOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddFindControlActionToTemplate(ATemplateFileName: Pointer;

                                        AActionName: Pointer;
                                        AActionTimeout: LongInt; //ms
                                        AActionEnabled: Boolean;
                                        AActionCondition: Pointer;

                                        AFindControlOptions: PClkFindControlOptionsAPI

                                       ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acFindControl, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetFindControlOptionsFromAPI(AFindControlOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddFindSubControlActionToTemplate(ATemplateFileName: Pointer;

                                           AActionName: Pointer;
                                           AActionTimeout: LongInt; //ms
                                           AActionEnabled: Boolean;
                                           AActionCondition: Pointer;

                                           AFindSubControlOptions: PClkFindSubControlOptionsAPI

                                          ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acFindSubControl, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetFindSubControlOptionsFromAPI(AFindSubControlOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddSetControlTextActionToTemplate(ATemplateFileName: Pointer;

                                           AActionName: Pointer;
                                           AActionTimeout: LongInt; //ms
                                           AActionEnabled: Boolean;
                                           AActionCondition: Pointer;

                                           ASetControlTextOptions: PClkSetTextOptionsAPI

                                          ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acSetControlText, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetSetControlTextOptionsFromAPI(ASetControlTextOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddCallTemplateActionToTemplate(ATemplateFileName: Pointer;

                                         AActionName: Pointer;
                                         AActionTimeout: LongInt; //ms
                                         AActionEnabled: Boolean;
                                         AActionCondition: Pointer;

                                         ACallTemplateOptions: PClkCallTemplateOptionsAPI

                                        ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acCallTemplate, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetCallTemplateOptionsFromAPI(ACallTemplateOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddSleepActionToTemplate(ATemplateFileName: Pointer;

                                  AActionName: Pointer;
                                  AActionTimeout: LongInt; //ms
                                  AActionEnabled: Boolean;
                                  AActionCondition: Pointer;

                                  ASleepOptions: PClkSleepOptionsAPI

                                 ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acSleep, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetSleepOptionsFromAPI(ASleepOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddSetVarActionToTemplate(ATemplateFileName: Pointer;

                                   AActionName: Pointer;
                                   AActionTimeout: LongInt; //ms
                                   AActionEnabled: Boolean;
                                   AActionCondition: Pointer;

                                   ASetVarOptions: PClkSetVarOptionsAPI

                                  ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acSetVar, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  Result := GetSetVarOptionsFromAPI(ASetVarOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
end;


function AddWindowOperationsActionToTemplate(ATemplateFileName: Pointer;

                                             AActionName: Pointer;
                                             AActionTimeout: LongInt; //ms
                                             AActionEnabled: Boolean;
                                             AActionCondition: Pointer;

                                             AWindowOperationsOptions: PClkWindowOperationsOptionsAPI

                                            ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acWindowOperations, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetWindowOperationsOptionsFromAPI(AWindowOperationsOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddLoadSetVarFromFileActionToTemplate(ATemplateFileName: Pointer;

                                               AActionName: Pointer;
                                               AActionTimeout: LongInt; //ms
                                               AActionEnabled: Boolean;
                                               AActionCondition: Pointer;

                                               ALoadSetVarFromFileOptions: PClkLoadSetVarFromFileOptionsAPI

                                              ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acLoadSetVarFromFile, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetLoadSetVarFromFileOptionsFromAPI(ALoadSetVarFromFileOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddSaveSetVarToFileActionToTemplate(ATemplateFileName: Pointer;

                                             AActionName: Pointer;
                                             AActionTimeout: LongInt; //ms
                                             AActionEnabled: Boolean;
                                             AActionCondition: Pointer;

                                             ASaveSetVarToFileOptions: PClkSaveSetVarToFileOptionsAPI

                                            ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acSaveSetVarToFile, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetSaveSetVarToFileOptionsFromAPI(ASaveSetVarToFileOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddPluginActionToTemplate(ATemplateFileName: Pointer;

                                   AActionName: Pointer;
                                   AActionTimeout: LongInt; //ms
                                   AActionEnabled: Boolean;
                                   AActionCondition: Pointer;

                                   APluginOptions: PClkPluginOptionsAPI

                                  ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acPlugin, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetPluginOptionsFromAPI(APluginOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddEditTemplateActionToTemplate(ATemplateFileName: Pointer;

                                         AActionName: Pointer;
                                         AActionTimeout: LongInt; //ms
                                         AActionEnabled: Boolean;
                                         AActionCondition: Pointer;

                                         AEditTemplateOptions: PClkEditTemplateOptionsAPI

                                        ): LongInt; cdecl;
var
  TemplateFileName: string;
  TempAction: TClkActionRec;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetBasicActionOptions(ATemplateFileName, AActionName, acEditTemplate, AActionTimeout, AActionEnabled, AActionCondition, TempAction, TemplateFileName);
  GetEditTemplateOptionsFromAPI(AEditTemplateOptions, TempAction);

  AddActionToTemplate(TemplateFileName, TempAction);
  Result := 0;
end;


function AddFontProfileToFindSubControlActionInTemplate(ATemplateFileName: string; ActionIndex: Integer; AFindSubControlMatchBitmapText: TClkFindControlMatchBitmapText): Integer;
var
  ClkActions: TClkActionsRecArr;
  MemStream: TMemoryStream;
  n: Integer;
  DummyNotes, DummyIconPath: string;
begin
  Result := 0;
  if not InMemFS.FileExistsInMem(ATemplateFileName) then
    CreateNewTemplateStr(ATemplateFileName);

  MemStream := TMemoryStream.Create;
  try
    InMemFS.LoadFileFromMemToStream(ATemplateFileName, MemStream);
    MemStream.Position := 0;

    GetTemplateContentFromMemoryStream(ClkActions, DummyNotes, DummyIconPath, MemStream);

    if (ActionIndex < 0) or (ActionIndex > Length(ClkActions) - 1) then
    begin
      Result := -2;
      Exit;
    end;

    n := Length(ClkActions[ActionIndex].FindSubControlOptions.MatchBitmapText);
    SetLength(ClkActions[ActionIndex].FindSubControlOptions.MatchBitmapText, n + 1);
    ClkActions[ActionIndex].FindSubControlOptions.MatchBitmapText[n] := AFindSubControlMatchBitmapText;

    MemStream.Clear;
    GetTemplateContentAsMemoryStream(ClkActions, DummyNotes, DummyIconPath, MemStream);
    InMemFS.SaveFileToMem(ATemplateFileName, MemStream.Memory, MemStream.Size);
  finally
    MemStream.Free;
  end;
end;


function AddFontProfileToFindSubControlAction(ATemplateFileName: Pointer;
                                              AActionIndex: LongInt;
                                              AFindControlMatchBitmapText: PClkFindControlMatchBitmapTextAPI
                                             ): LongInt; cdecl;
var
  TemplateFileName: string;
  FindControlMatchBitmapText: TClkFindControlMatchBitmapText;
begin
  if InMemFS = nil then
  begin
    Result := -1;
    Exit;
  end;

  SetPointedContentToString(@string(PWideChar(ATemplateFileName))[1], TemplateFileName);

  if not InMemFS.FileExistsInMem(TemplateFileName) then
  begin
    Result := -1;
    Exit;
  end;

  GetMatchBitmapTextFromAPI(AFindControlMatchBitmapText, FindControlMatchBitmapText);

  Result := AddFontProfileToFindSubControlActionInTemplate(TemplateFileName, AActionIndex, FindControlMatchBitmapText);
end;


function ExecuteRemoteActionAtIndex(AActionIndex, AStackLevel: Integer; AVarReplacements: TStringList): Boolean;
const
  CUsesInMemFiles: array[Boolean] of string = (CREParam_FileLocation_ValueDisk, CREParam_FileLocation_ValueMem);
var
  Th: TClientThread;
  ServerResponse: string;
begin
  Result := False;

  Th := TClientThread.Create(True);   //without using thread, the client blocks both this UI and the server's UI, because it doesn't read  - some sort of deadlock
  try
    Th.ConnectTimeout := GeneralConnectTimeout;
    Th.Link := FRemoteAddress + CRECmd_ExecuteCommandAtIndex + '?' +
                                CREParam_ActionIdx + '=' + IntToStr(AActionIndex) + '&' +
                                CREParam_StackLevel + '=' + IntToStr(AStackLevel) + '&' +
                                CREParam_IsDebugging + '=' + IntToStr(Ord(False)) + '&' +  //useful for debugging plugins
                                CREParam_FileLocation + '= ' + CUsesInMemFiles[False]; //this should come from a class field
    Th.Start;

    WaitForServerResponse(Th);

    ServerResponse := FastReplace_87ToReturn(Th.Result);

    if (Trim(ServerResponse) = '') or (Pos('$Control_Class$', ServerResponse) = 0) then // uses '$Control_Class$' as a way to find out if the result is a valid list of variables
      AVarReplacements.Values['$ServerResponse$'] := ServerResponse
    else
      AVarReplacements.Text := ServerResponse;

    Result := AVarReplacements.Values[CREResp_RemoteExecResponseVar] = '1';
  finally
    Th.Free;
  end;
end;


function PrepareFilesInServer(ATemplateFileName, AResponse: Pointer): LongInt; cdecl;
const
  CStackLevel: Integer = 0;  //Load only the main file. The others should be automatically handled by server.
var
  TemplateFileName: string;
  ClkActions: TClkActionsRecArr;
  MemStream: TMemoryStream;
  SetClientResponse: string;
  DummyNotes, DummyIconPath: string;
begin
  Result := 0;
  SetClientResponse := '';

  SetPointedContentToString(@string(PWideChar(ATemplateFileName))[1], TemplateFileName);
  if TemplateFileName = '' then
    Exit;

  try
    if GetServerFileExpectancy(FRemoteAddress) = CREResp_FileExpectancy_ValueFromClient then
    begin
      //Should send template and other files to server...
      if not InMemFS.FileExistsInMem(TemplateFileName) then
        CreateNewTemplateStr(TemplateFileName);

      MemStream := TMemoryStream.Create;
      try
        InMemFS.LoadFileFromMemToStream(TemplateFileName, MemStream);
        MemStream.Position := 0;

        GetTemplateContentFromMemoryStream(ClkActions, DummyNotes, DummyIconPath, MemStream);

        SetClientResponse := 'LoadTemplateInExecList: ' + SetClientTemplateInServer(FRemoteAddress, TemplateFileName, ClkActions, CStackLevel);
        SetClientResponse := SetClientResponse + ',  SendMissingFilesToServer: ' + SendMissingFilesToServer(FRemoteAddress, ClkActions);

        Result := 0;
      finally
        MemStream.Free;
      end;
    end;

    Result := SetPointedContentFromString(SetClientResponse, AResponse);
  except
    on E: Exception do
      Result := SetPointedContentFromString(E.Message, AResponse);
  end;
end;


function ExecuteActionAtIndex(AActionIndex, AStackLevel: LongInt): Boolean; cdecl;
begin
  Result := ExecuteRemoteActionAtIndex(AActionIndex, AStackLevel, FAllVarReplacements);
end;


function GetListOfFilesFromClientInMem(AResponse: Pointer): LongInt; cdecl;
var
  ListOfFiles: TStringList;
begin
  ListOfFiles := TStringList.Create;
  try
    try
      InMemFS.ListMemFiles(ListOfFiles);
      Result := SetPointedContentFromString(ListOfFiles.Text, AResponse);
    except
      on E: Exception do
        Result := SetPointedContentFromString(E.Message, AResponse);
    end;
  finally
    ListOfFiles.Free;
  end;
end;


function GetTemplateContentFromClientInMemAsString(ATemplateFileName, AResponse: Pointer): LongInt; cdecl;
var
  FileContent: TStringList;
  TemplateFileName: string;
  Buffer: TBytes;
  Dest: Pointer;
begin
  Result := 0;
  SetPointedContentToString(@string(PWideChar(ATemplateFileName))[1], TemplateFileName);
  if TemplateFileName = '' then
    Exit;

  if not InMemFS.FileExistsInMem(TemplateFileName) then
  begin
    Result := SetPointedContentFromString('File not found.', AResponse);
    Exit;
  end;

  FileContent := TStringList.Create;
  try
    try
      Result := InMemFS.GetFileSize(TemplateFileName);
      SetLength(Buffer, Result);

      Dest := @Buffer[0];
      InMemFS.LoadFileFromMem(TemplateFileName, Dest);
      Move(Dest^, AResponse^, Min(Result, CMaxSharedStringLength)); //limit to 10MB
    except
      on E: Exception do
        Result := SetPointedContentFromString(E.Message, AResponse);
    end;
  finally
    FileContent.Free;
  end;
end;


function StopTemplateExecution(AStackLevel: LongInt; AResponse: Pointer): LongInt; cdecl;
var
  Resp: string;
begin
  Resp := StopRemoteTemplateExecution(FRemoteAddress, AStackLevel);
  Result := SetPointedContentFromString(Resp, AResponse);
end;


procedure StartFileProviderClientThread; cdecl;
begin
  FPollForMissingServerFilesTh.Start;
end;


procedure TerminateFileProviderClientThread; cdecl;
begin
  FPollForMissingServerFilesTh.Terminate;
end;


procedure AddListOfAccessibleDirsToFileProviderClient(AListOfDirs: Pointer); cdecl;
var
  AList: TStringList;
  ListAsString: string;
begin
  AList := TStringList.Create;
  try
    SetPointedContentToString(@string(PWideChar(AListOfDirs))[1], ListAsString);
    AList.Text := ListAsString;

    FPollForMissingServerFilesTh.AddListOfAccessibleDirs(AList);
  finally
    AList.Free;
  end;
end;


procedure AddListOfAccessibleFileExtensionsToFileProviderClient(AListOfExtensions: Pointer); cdecl;
var
  AList: TStringList;
  ListAsString: string;
begin
  AList := TStringList.Create;
  try
    SetPointedContentToString(@string(PWideChar(AListOfExtensions))[1], ListAsString);
    AList.Text := ListAsString;

    FPollForMissingServerFilesTh.AddListOfAccessibleFileExtensions(AList);
  finally
    AList.Free;
  end;
end;


function FileProviderClientThreadDone: Boolean; cdecl;
begin
  Result := FPollForMissingServerFilesTh.Done;
end;


procedure SetFileProviderClientConnectTimeout(ATimeout: LongInt); cdecl; //should be called before StartFileProviderClientThread to actually use the new timeout
begin
  if FPollForMissingServerFilesTh = nil then
    Exit;

  FPollForMissingServerFilesTh.ConnectTimeout := ATimeout;
end;


function SendMemPluginFileToServer(AFileName, AFileContent: Pointer; AFileSize: Int64; AResultStr: Pointer): Integer; cdecl;
var
  Res: string;
  FileName: string;
  MemStream: TMemoryStream;
begin
  SetPointedContentToString(@string(PWideChar(AFileName))[1], FileName);
  Res := '';
  try
    MemStream := TMemoryStream.Create;
    try
      MemStream.SetSize(AFileSize);
      Move(AFileContent^, MemStream.Memory^, AFileSize);
      Res := SendMemPluginFile(FRemoteAddress, FileName, MemStream, True);
    finally
      MemStream.Free;
    end;

    Result := SetPointedContentFromString(Res, AResultStr);
  except
    on E: Exception do
    begin
      AddToLog('Can''t send plugin file to server: "' + E.Message + '"  Res: ' + Res);
      try
        Result := SetPointedContentFromString('AV in ClickerClient: ' + E.Message + '  AFileContent: $' + IntToHex(UInt64(AFileContent)), AResultStr);
      except
      end;

      Sleep(3000);
    end;
  end;
end;


function SendMemPluginArchiveFileToServer(AFileName, ADecryptionPluginName, ADecompressionPluginName, AHashingPluginName, AFileContent: Pointer; AFileSize: Int64; ACompressionLevel: Integer; AAdditionalInfo: Pointer; AIsDecDecHash: Boolean; AResultStr: Pointer): Integer; cdecl;
var
  Res: string;
  FileName, DecryptionPluginName, DecompressionPluginName, HashingPluginName, AdditionalInfo: string;
  MemStream: TMemoryStream;
begin
  SetPointedContentToString(@string(PWideChar(AFileName))[1], FileName);
  SetPointedContentToString(@string(PWideChar(ADecryptionPluginName))[1], DecryptionPluginName);
  SetPointedContentToString(@string(PWideChar(ADecompressionPluginName))[1], DecompressionPluginName);
  SetPointedContentToString(@string(PWideChar(AHashingPluginName))[1], HashingPluginName);
  SetPointedContentToString(@string(PWideChar(AAdditionalInfo))[1], AdditionalInfo);

  Res := '';
  try
    MemStream := TMemoryStream.Create;
    try
      MemStream.SetSize(AFileSize);
      Move(AFileContent^, MemStream.Memory^, AFileSize);
      Res := SendMemPluginArchiveFile(FRemoteAddress, FileName, DecryptionPluginName, DecompressionPluginName, HashingPluginName, MemStream, ACompressionLevel, AdditionalInfo, AIsDecDecHash, True);
    finally
      MemStream.Free;
    end;

    Result := SetPointedContentFromString(Res, AResultStr);
  except
    on E: Exception do
    begin
      AddToLog('Can''t send plugin archive file to server: "' + E.Message + '"  Res: ' + Res);
      try
        Result := SetPointedContentFromString('AV in ClickerClient: ' + E.Message + '  AFileContent: $' + IntToHex(UInt64(AFileContent)), AResultStr);
      except
      end;

      Sleep(3000);
    end;
  end;
end;


procedure ClearClientInMemFS; cdecl;
begin
  InMemFS.Clear;
end;


procedure ClearServerInMemFS; cdecl;
begin
  ClearInMemFileSystem(FRemoteAddress);
end;


function ExecuteClickAction(AActionName: Pointer;
                            AActionTimeout: LongInt; //ms
                            AClickOptions: PClkClickOptionsAPI;
                            AUseServerDebugging: Boolean;
                            AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
begin
  GetClickOptionsFromAPI(AClickOptions, TempClkAction);
  Res := ClickerActionsClient.ExecuteClickAction(FRemoteAddress, TempClkAction.ClickOptions, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteExecAppAction(AActionName: Pointer;
                              AActionTimeout: LongInt; //ms
                              AExecAppOptions: PClkExecAppOptionsAPI;
                              AUseServerDebugging: Boolean;
                              AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetExecAppOptionsFromAPI(AExecAppOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecuteExecAppAction(FRemoteAddress, TempClkAction.ExecAppOptions, ActionName, AActionTimeout, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteFindControlAction(AActionName: Pointer;
                                  AActionTimeout: LongInt; //ms
                                  AFindControlOptions: PClkFindControlOptionsAPI;
                                  AUseServerDebugging: Boolean;
                                  AFileLocation: Pointer;
                                  AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
  FileLocation: string;
begin
  GetFindControlOptionsFromAPI(AFindControlOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  SetPointedContentToString(@string(PWideChar(AFileLocation))[1], FileLocation);
  Res := ClickerActionsClient.ExecuteFindControlAction(FRemoteAddress, TempClkAction.FindControlOptions, ActionName, AActionTimeout, FileLocation, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteFindSubControlAction(AActionName: Pointer;
                                     AActionTimeout: LongInt; //ms
                                     AFindSubControlOptions: PClkFindSubControlOptionsAPI;
                                     AUseServerDebugging: Boolean;
                                     AFileLocation: Pointer;
                                     AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
  FileLocation: string;
begin
  GetFindSubControlOptionsFromAPI(AFindSubControlOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  SetPointedContentToString(@string(PWideChar(AFileLocation))[1], FileLocation);
  Res := ClickerActionsClient.ExecuteFindSubControlAction(FRemoteAddress, TempClkAction.FindSubControlOptions, ActionName, AActionTimeout, FileLocation, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteSetControlTextAction(AActionName: Pointer;
                                     AActionTimeout: LongInt; //ms
                                     ASetControlTextOptions: PClkSetTextOptionsAPI;
                                     AUseServerDebugging: Boolean;
                                     AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetSetControlTextOptionsFromAPI(ASetControlTextOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecuteSetControlTextAction(FRemoteAddress, TempClkAction.SetTextOptions, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteCallTemplateAction(AActionName: Pointer;
                                   AActionTimeout: LongInt; //ms
                                   ACallTemplateOptions: PClkCallTemplateOptionsAPI;
                                   AUseServerDebugging: Boolean;
                                   AFileLocation: Pointer;
                                   AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
  FileLocation: string;
begin
  GetCallTemplateOptionsFromAPI(ACallTemplateOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  SetPointedContentToString(@string(PWideChar(AFileLocation))[1], FileLocation);
  Res := ClickerActionsClient.ExecuteCallTemplateAction(FRemoteAddress, TempClkAction.CallTemplateOptions, False, False, FileLocation, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteSleepAction(AActionName: Pointer;
                            AActionTimeout: LongInt; //ms
                            ASleepOptions: PClkSleepOptionsAPI;
                            AUseServerDebugging: Boolean;
                            AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetSleepOptionsFromAPI(ASleepOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecuteSleepAction(FRemoteAddress, TempClkAction.SleepOptions, ActionName, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteSetVarAction(AActionName: Pointer;
                             AActionTimeout: LongInt; //ms
                             ASetVarOptions: PClkSetVarOptionsAPI;
                             AUseServerDebugging: Boolean;
                             AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetSetVarOptionsFromAPI(ASetVarOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecuteSetVarAction(FRemoteAddress, TempClkAction.SetVarOptions, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteWindowOperationsAction(AActionName: Pointer;
                                       AActionTimeout: LongInt; //ms
                                       AWindowOperationsOptions: PClkWindowOperationsOptionsAPI;
                                       AUseServerDebugging: Boolean;
                                       AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetWindowOperationsOptionsFromAPI(AWindowOperationsOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecuteWindowOperationsAction(FRemoteAddress, TempClkAction.WindowOperationsOptions, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteLoadSetVarFromFileAction(AActionName: Pointer;
                                         AActionTimeout: LongInt; //ms
                                         ALoadSetVarFromFileOptions: PClkLoadSetVarFromFileOptionsAPI;
                                         AUseServerDebugging: Boolean;
                                         AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetLoadSetVarFromFileOptionsFromAPI(ALoadSetVarFromFileOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecuteLoadSetVarFromFileAction(FRemoteAddress, TempClkAction.LoadSetVarFromFileOptions, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteSaveSetVarToFileAction(AActionName: Pointer;
                                       AActionTimeout: LongInt; //ms
                                       ASaveSetVarToFileOptions: PClkSaveSetVarToFileOptionsAPI;
                                       AUseServerDebugging: Boolean;
                                       AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetSaveSetVarToFileOptionsFromAPI(ASaveSetVarToFileOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecuteSaveSetVarToFileAction(FRemoteAddress, TempClkAction.SaveSetVarToFileOptions, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecutePluginAction(AActionName: Pointer;
                             AActionTimeout: LongInt; //ms
                             APluginOptions: PClkPluginOptionsAPI;
                             AUseServerDebugging: Boolean;
                             AUseStepIntoDebugging: Boolean;
                             AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetPluginOptionsFromAPI(APluginOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecutePluginAction(FRemoteAddress, TempClkAction.PluginOptions, True, AUseServerDebugging, AUseStepIntoDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


function ExecuteEditTemplateAction(AActionName: Pointer;
                                   AActionTimeout: LongInt; //ms
                                   AEditTemplateOptions: PClkEditTemplateOptionsAPI;
                                   AUseServerDebugging: Boolean;
                                   AResultStr: Pointer): LongInt; cdecl;
var
  TempClkAction: TClkActionRec;
  Res: string;
  ActionName: string;
begin
  GetEditTemplateOptionsFromAPI(AEditTemplateOptions, TempClkAction);
  SetPointedContentToString(@string(PWideChar(AActionName))[1], ActionName);
  Res := ClickerActionsClient.ExecuteEditTemplateAction(FRemoteAddress, TempClkAction.EditTemplateOptions, True, AUseServerDebugging);
  Result := SetPointedContentFromString(Res, AResultStr);
end;


exports
  InitClickerClient,
  DoneClickerClient,
  SetServerAddress,
  GetServerAddress,
  TestConnectionToServer,
  SetTemplatesDir,
  CreateLoggingWindow,
  DestroyLoggingWindow,

  CreateNewTemplate,

  AddClickActionToTemplate,
  AddExecAppActionToTemplate,
  AddFindControlActionToTemplate,
  AddFindSubControlActionToTemplate,
  AddSetControlTextActionToTemplate,
  AddCallTemplateActionToTemplate,
  AddSleepActionToTemplate,
  AddSetVarActionToTemplate,
  AddWindowOperationsActionToTemplate,
  AddLoadSetVarFromFileActionToTemplate,
  AddSaveSetVarToFileActionToTemplate,
  AddPluginActionToTemplate,
  AddEditTemplateActionToTemplate,

  AddFontProfileToFindSubControlAction,

  PrepareFilesInServer,
  ExecuteActionAtIndex,
  GetListOfFilesFromClientInMem,
  GetTemplateContentFromClientInMemAsString,
  StopTemplateExecution,

  StartFileProviderClientThread,
  TerminateFileProviderClientThread,
  AddListOfAccessibleDirsToFileProviderClient,
  AddListOfAccessibleFileExtensionsToFileProviderClient,
  FileProviderClientThreadDone,
  SetFileProviderClientConnectTimeout,
  SendMemPluginFileToServer,
  SendMemPluginArchiveFileToServer,

  ClearClientInMemFS,
  ClearServerInMemFS,

  ExecuteClickAction,
  ExecuteExecAppAction,
  ExecuteFindControlAction,
  ExecuteFindSubControlAction,
  ExecuteSetControlTextAction,
  ExecuteCallTemplateAction,
  ExecuteSleepAction,
  ExecuteSetVarAction,
  ExecuteWindowOperationsAction,
  ExecuteLoadSetVarFromFileAction,
  ExecuteSaveSetVarToFileAction,
  ExecutePluginAction,
  ExecuteEditTemplateAction;
begin
  ClientLastError := '';
  frmClickerClientLogging := nil;
end.

